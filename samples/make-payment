#! /usr/bin/env coffee

# Required modules

FS = require('fs')
Util = require('util')
YaMoney = require('..')

# Check provided CLI args

if process.argv.length < 3
	programName = process.argv[1].match(/[^/]+$/)[0]

	console.log('Creates new payment and processes it')
	console.log('Usage: ' + programName + ' TOKEN_PATH [PARAM1=VALUE1]...')

	process.exit()

# Read token from file pointed in CLI args

tokenPath = process.argv[2]
token = JSON.parse(FS.readFileSync(tokenPath, encoding: 'utf-8')).access_token

# Generate request input data from CLI args

params = Object.create(null)

for pair in process.argv.slice(3)
	[key, value] = pair.split('=')
	params[key] = value

# Create client using specified token

client = new YaMoney.Client(token: token)

# And invoke remote methods

client.requestPayment(params, (error, data) ->
	unless error?
		console.log(Util.inspect(data, colors: true))
	else
		console.log(error)

	return if error?

	client.processPayment(null, (error, data) ->
		unless error?
			console.log(Util.inspect(data, colors: true, depth: null))
		else
			console.log(error)

		return
	)

	return
)
